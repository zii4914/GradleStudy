//指定该module构建的类型，有application及library两种
apply plugin: 'com.android.application'

//android{}指定构建配置
android {
    //签名配置
    signingConfigs {
        config {
            keyAlias 'study-android'
            keyPassword 'zii4914'
            storeFile file('C:/ZII/Practice/GradleStudy/app/zii_study_key.jks')
            storePassword 'zii4914'
        }
    }
    //compileSdkVersion指定编译app时候使用的Android API level(即android版本)
    compileSdkVersion 26
    //Build Tools 26.0.2 or higher. With this update, you no longer need to specify a version for
    // the build tools—the pluginuses the minimum required version by default. So,
    // you can now remove the android.buildToolsVersion property.【buildToolsVersion在Studio3.0中已经可以去除指定，Studio默认指定使用最低需求的版本】

    //defaultConfig{}为所有的构建变体提供默认的配置，可以通过配置productFlavors来复写其中的配置
    defaultConfig {
        //app的唯一标志，应用市场只根据该id是否相同判断同一个app，默认是初始包名结构【需要生成不同app，修改该处就可以，跟Manifest的package及包名无关】
        applicationId "com.zii.study.gradlestudy"
        //定义app所支持的最低Android API版本(android系统版本)
        minSdkVersion 19
        //表示app在该Android API版本(android系统版本)经过充分的测试，系统完全信任app，系统不再做兼容处理(如动态权限请求API-23)
        targetSdkVersion 26
        //app实际版本号
        versionCode 1
        //用于展示给用户看到的版本名称
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    //可以在这里配置多个构建类型。默认有release及debug，但是debug默认不显示
    buildTypes {
        release {
            //是否进行混淆
            minifyEnabled false
            //删除无效资源
            shrinkResources false
            //混淆文件的位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //指定签名
            signingConfig signingConfigs.config
        }
        debug {
            //是否进行混淆
            minifyEnabled false
            //删除无效资源
            shrinkResources false
            //混淆文件的位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //指定签名
            signingConfig signingConfigs.config
        }
    }

    //flavorDimensions该属性可以定义构建产品风味组
    //默认情况下在产品风味中不定义dimension，并且flavorDimensions随便设置，则默认构建flavor + buildType 变体(共4*2)
    //若如下配置version及api，则以version flavor + api flavor +build type 的方式进行组合(共2*2*2)
    flavorDimensions 'version', 'api'
    //这里可选，默认不生成。在这里可以配置产品风味，为app创建不同的版本(允许指定applicationId等属性)，这里的配置覆盖defaultConfig。
    productFlavors {
        home {
            dimension 'version'
            //普通版
            applicationIdSuffix '.home'

        }
        pro {
            dimension 'version'
            //专业版
            applicationIdSuffix '.pro'
        }

        minApi19 {
            dimension 'api'
            minSdkVersion 19
            versionNameSuffix '-minApi19'
        }
        minApi21 {
            dimension 'api'
            minSdkVersion 21
            versionNameSuffix '-minApi21'
        }
    }
    //过滤构建变体(eg.pro版仅支持api21+)
    variantFilter { variant ->
        def names = variant.flavors*.name
        //忽略pro的minApi21版本
        if (names.contains("minApi19") && names.contains("pro")) {
            setIgnore(true)
        }
    }

    //定制输出的apk的文件名(即指定所有变体输出的文件名称)
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def formattedDate = new Date().format('yyyyMMddHHmmss')

                def fileName = "${variant.applicationId}" +
                        "-${variant.getBuildType().getName()}" +
                        "-${variant.flavorName}" +
                        "-${variant.versionName}" +
                        "-${variant.versionCode}" +
                        "-${formattedDate}.apk"
                outputFileName = fileName
//                outputFileName = new File(outputFile.parent, fileName)
            }
        }
    }

    //指定默认源集配置 -- https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.api.AndroidSourceSet.html
    sourceSets {
        //main源集
        main {
            //aidl  assets java manifest 等；路径是相对于模块级别build.gradle的文件路径
            res.srcDirs = ['src/main/res']  //资源目录
            jniLibs.srcDirs = ['src/main/jniLibs']  //jni库目录
            jni.srcDirs = []    //jni源码目录

        }
        androidTest {
            //指定源集根目录
            setRoot("src/androidTest")
        }
        pro {
            //pro源集的res资源指向proDebug源集的res
            //Note:这样指定，那么ImageView的图片就指向proDebug的res资源图片，letter显示是藏青色P字母，而不是pro下的的青蓝色P
            res.srcDir("res")
            setRoot("src/proDebug")
        }
    }
}

dependencies {
    //Note:Studio 3.0后，取消compile关键字，取而代之是api，implementation
    //implementation，eg. library中implementation 了gson库，但是app引入了library，但是无法使用gson，若需要使用需要换回api(api==compile)，好处是加快编译速度

    //本地二进制依赖项，即Jar包
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //远程二进制依赖项，即远程第三方库
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
